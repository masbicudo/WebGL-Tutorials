<head>
    <title>#17 Border Anti-aliasing</title>
    <script type="text/javascript" src="../lib/gl-matrix.js"></script>
    <script type="text/javascript" src="../lib/mdn-getShader.js"></script>
    <script type="text/javascript" src="../lib/leaf-draw.js"></script>
    <link rel="stylesheet" type="text/css" href="editors.css">
</head>
<body>
  <div style="display: inline-block;"><canvas id="canvas" style="border:none" width="600" height="600" /></div>
  <div style="display: inline-block;">
     <div id="txt">0</div>
     <div class="row"><div class="group">Settings</div>
        <div class="row"><label for="uEnableAA">Enable AA</label> <input id="uEnableAA" type="checkbox" checked="checked" /></div>
        <div class="row"><label for="uAnimationSpeed">Animation speed</label> <input id="uAnimationSpeed" type="textbox" value="1" /></div>
        <div class="row"><label for="uAAMinAngleSine">AA Min Angle Sine</label> <input id="uAAMinAngleSine" type="textbox" value="0.99" /></div>
        <div class="row"><label for="uAAMaxSteps">AA Max Steps</label> <input id="uAAMaxSteps" type="textbox" value="2" /></div>
      </div>
  </div>
</body>
<script type="text/javascript">
  function startWebGLApp2() {
    startWebGLApp({
      uEnableAA: {type: "bool"},
      uAAMinAngleSine: {type: "float"},
      uAnimationSpeed: {type: "float"},
      uAAMaxSteps: {type: "int"},
      uPixelSize: {type: "--pixel-size"},
    });
  }
  if(window.addEventListener) {
    window.addEventListener('load', startWebGLApp2)
  }else{
    window.attachEvent('onload', startWebGLApp2)
  }
</script>
<script id="fragment-shader" type="x-shader/x-fragment">
  precision highp float;
  const int MAX_STEPS = 100;
  const float CONTACT_DIFF = 2.0/1024.0;
  const float NORMAL_GRADIENT_DIFF = 32./1024./1024.;
  const float LIGHT_AMBIENT = 0.075;
  const vec3 LIGHT_POINT = vec3(-3.0, 0.7, -0.7);
  const float BORDER_DISTANCE = 1.5e-2;

  const float OBJ1_MATERIAL_SHINESS = 15.;
  const float OBJ1_LIGHT_INTENSITY = 9.;
  const vec3 OBJ1_COLOR = vec3(1.0, 0.5, 0.5);
  const vec3 OBJ1_BORDER_COLOR = vec3(1.0, 0.05, 0.05);

  const float OBJ2_MATERIAL_SHINESS = 0.4;
  const float OBJ2_LIGHT_INTENSITY = 6.;
  const vec3 OBJ2_COLOR = vec3(0.5, 1.0, 0.5);
  const vec3 OBJ2_BORDER_COLOR = vec3(0.05, 1.0, 0.05);

  uniform float uAnimationSpeed;
  uniform float uAAMinAngleSine;
  uniform int uAAMaxSteps;
  uniform bool uEnableAA;

  uniform vec2 uPixelSize;

  varying vec4 pos;

  uniform float uTime;

  const float PI = 3.14159265359;
  const float Deg2Rad = 2.*PI/360.;

  float map(float value, float low1, float high1, float low2, float high2) {
    return low2 + (value - low1) * (high2 - low2) / (high1 - low1);
  }

  float distCircleNx(float n, vec2 p, vec2 c, float r) {
    vec2 k = p - c;
    return pow(pow(abs(k.x), n) + pow(abs(k.y), n), 1./n) - r;
  }
  vec2 mapPolarNx(float n, vec3 p) {
    return vec2(p.x, pow(pow(abs(p.y), n) + pow(abs(p.z), n), 1./n));
  }
  float distTorusNxx(float nt, float nc, vec3 p, float rt, float rc) {
    return distCircleNx(nc, mapPolarNx(nt, p), vec2(0., rt), rc);
  }

  vec3 rotateAroundY(vec3 v, float a) {
    float c_a = cos(a);
    float s_a = sin(a);
    mat4 T = mat4(
      c_a, 0.0, s_a, 0.0,
      0.0, 1.0, 0.0, 0.0,
     -s_a, 0.0, c_a, 0.0,
      0.0, 0.0, 0.0, 1.0
      );
    return (T * vec4(v, 1.0)).xyz;
  }
  vec3 rotateAroundX(vec3 v, float a) {
    float c_a = cos(a);
    float s_a = sin(a);
    mat4 T = mat4(
      1.0, 0.0, 0.0, 0.0,
      0.0, c_a,-s_a, 0.0,
      0.0, s_a, c_a, 0.0,
      0.0, 0.0, 0.0, 1.0
      );
    return (T * vec4(v, 1.0)).xyz;
  }
  vec3 translate(vec3 v, vec3 d) {
    mat4 T = mat4(
      1.0, 0.0, 0.0, 0.0,
      0.0, 1.0, 0.0, 0.0,
      0.0, 0.0, 1.0, 0.0,
      d.x, d.y, d.z, 1.0
      );
    return (T * vec4(v, 1.0)).xyz;
  }
  vec3 scale(vec3 v, vec3 s) {
    mat4 T = mat4(
      s.x, 0.0, 0.0, 0.0,
      0.0, s.y, 0.0, 0.0,
      0.0, 0.0, s.z, 0.0,
      0.0, 0.0, 0.0, 1.0
      );
    return (T * vec4(v, 1.0)).xyz;
  }

  float distFunc(vec3 v, out int objectId) {
    float time = uTime*uAnimationSpeed;
    v = translate(v, vec3(0.0, 0.0, -1.0));
    v = rotateAroundX(v, (time + 60.0)*Deg2Rad);
    v = rotateAroundY(v, (time + 30.0)*Deg2Rad);

    vec3 v1 = v;
    v1 = translate(v1, vec3(0.,0.275,0.));
    v1 = rotateAroundX(v1, 90.*Deg2Rad);
    v1 = rotateAroundY(v1, 0.*Deg2Rad);
    float d1 = distTorusNxx(2., 2., v1, 0.4, 0.1);

    vec3 v2 = v;
    v2 = translate(v2, vec3(0.,-0.275,0.0));
    v2 = rotateAroundX(v2, -0.*Deg2Rad);
    v2 = rotateAroundY(v2, -90.*Deg2Rad);
    float d2 = distTorusNxx(10., 10., v2, 0.4, 0.1);

    if (d1 < d2) {
      objectId = 1;
    }
    else {
      objectId = 2;
    }
    return min(d1, d2);
  }

  float lightMarch(in vec3 origin, in vec3 direction, float maxTravel, float spread) {
    float traveled = 0.0;
    int steps = 0;
    float res = 0.9;
    float prevDist = 1000.0;
    int objectId;
    for (int i = 0; i < MAX_STEPS; i++) {
      steps++;
      float dist = distFunc(origin + traveled*direction, objectId);
      if (dist < CONTACT_DIFF)
        break;
      if (dist >= prevDist)
        res = min(res, spread*prevDist/(maxTravel - traveled));
      traveled = traveled + dist;
      if (traveled >= maxTravel)
        break;
      prevDist = dist;
    }
    if (maxTravel - traveled >= CONTACT_DIFF * 32.) {
      return 0.1;
    }
    else {
      return 0.1 + res;
    }
  }

  vec3 normalFunc(vec3 point, float pointDistance) {
    vec3 pX = vec3(point.x + NORMAL_GRADIENT_DIFF, point.y, point.z);
    vec3 pY = vec3(point.x, point.y + NORMAL_GRADIENT_DIFF, point.z);
    vec3 pZ = vec3(point.x, point.y, point.z + NORMAL_GRADIENT_DIFF);
    int objectId2;
    float distX = distFunc(pX, objectId2);
    float distY = distFunc(pY, objectId2);
    float distZ = distFunc(pZ, objectId2);

    // this will give the surface normal for the fragment
    vec3 normal = normalize(vec3(
      distX - pointDistance,
      distY - pointDistance,
      distZ - pointDistance));
    return normal;
  }

  struct RayMarchResult {
    float nearestDistance;
    float travelToNearest;
    int nearestId;
    bool culled;
    int steps;
  };

  RayMarchResult raymarch(vec3 rayOrigin, vec3 rayDirection) {
  
    float nearestDistance = 1000.0;
    float travelToNearest = 0.0;
    int nearestId;

    int borderId = -1;
    int steps = 0;
    float traveledSoFar = 0.0;
    for (int i = 0; i < MAX_STEPS; i++) {
      steps++;
      int objectId;
      float dist = distFunc(rayOrigin + traveledSoFar*rayDirection, objectId);
      if (borderId < 0 && dist < BORDER_DISTANCE) {
	borderId = objectId;
      }
      if (dist < nearestDistance) {
	nearestDistance = dist;
	travelToNearest = traveledSoFar;
	nearestId = objectId;
      }
      if (traveledSoFar > 2.0)
	break;
      if (dist < CONTACT_DIFF)
	break;
      traveledSoFar += dist;
    }
    
    return RayMarchResult(
      nearestDistance,
      travelToNearest,
      nearestId,
      traveledSoFar > 2.0,
      steps);
  }

  void mainShaded(void) {
    vec3 fragColor = vec3(0.0);

    vec3 eyeRayOrigin = vec3(pos.xy, 0.0);
    vec3 eyeRayDirection = vec3(0.0, 0.0, 1.0);
    int repeat = 1;

    const int MAX_AA_STEPS = 20;
    for (int runId = 0; runId < MAX_AA_STEPS; runId++) {
      if (runId >= uAAMaxSteps) break;
      if (runId >= repeat) break;
      RayMarchResult r = raymarch(eyeRayOrigin, eyeRayDirection);

      int addToRepeat = 0;
      if (r.culled) {
        fragColor += vec3(0.0);
      }
      else {
        vec3 p = eyeRayOrigin + eyeRayDirection*r.travelToNearest;

        vec3 normal = vec3(0.0, 0.0, 0.0);
        if (r.nearestDistance < CONTACT_DIFF || r.steps == MAX_STEPS) {
          normal = normalFunc(p, r.nearestDistance);

          // deflecting eye-ray with the normal
          vec3 normalOrthoEyeDir = normal - eyeRayDirection * (dot(normal, eyeRayDirection) / dot(eyeRayDirection, eyeRayDirection));

          // normalOrthoEyeDir is smaller than or equal to the normal,
          // the length of the normal is 1.0, because it is normalized,
          // then the length of normalOrthoEyeDir is smaller than or equal to 1.0.
          if (length(normalOrthoEyeDir) > uAAMinAngleSine)
          {
            eyeRayOrigin = p + vec3(uPixelSize, 0.5*(uPixelSize.x + uPixelSize.y))*normalOrthoEyeDir; // alternative #1
            //eyeRayOrigin = p + uPixelSize.x*normal; // alternative #2
            //fragColor = vec3(normalOrthoEyeDir * 0.5 + 0.5);
            //return;
            addToRepeat = 1;
          }
        }

        vec3 lightPos = LIGHT_POINT;
        vec3 lightVec = (lightPos - p);
        vec3 lightDir = normalize(lightVec);

        // march from the light to the point p
        // if any body is found, then there is no light
        // if the light passes near a body, then there is partial light
        float lightSource = lightMarch(lightPos, -lightDir, length(lightVec), 16.);

        // Diffuse
        float lightIntensity = (r.nearestId == 1 ? OBJ1_LIGHT_INTENSITY : OBJ2_LIGHT_INTENSITY) / dot(lightVec, lightVec);
        float diffuse = max(dot(normal, lightDir), 0.0);

        // SpeculartravelToNearest
        vec3 viewDir = normalize(vec3(pos.xy, 0.0) - p);
        vec3 reflectDir = reflect(-lightDir, normal);
        float specular = pow(max(dot(viewDir, reflectDir), 0.0), (r.nearestId == 1 ? OBJ1_MATERIAL_SHINESS : OBJ2_MATERIAL_SHINESS));

        vec3 objectColor = r.nearestId == 1 ? OBJ1_COLOR : OBJ2_COLOR;

        float lightness = clamp((specular + diffuse) * lightSource * lightIntensity + LIGHT_AMBIENT, 0.0, 1.0);
        fragColor += vec3(lightness) * objectColor;
      }

      repeat += addToRepeat;
      if (!uEnableAA) break;
    }

    gl_FragColor = vec4(fragColor / float(!uEnableAA ? 1 : repeat > uAAMaxSteps ? uAAMaxSteps : repeat), 1.0);
    //gl_FragColor = vec4(vec3((float(repeat-1.0) * 0.1) + 0.00001), 1.0);
  }
  void main(void) {
    mainShaded(); return;
  }
</script>
<script id="vertex-shader" type="x-shader/x-vertex">
  attribute vec2 aVertexCoords;
  varying vec4 pos;
  uniform mat4 uTransform;

  void main(void) {
    gl_Position = vec4(aVertexCoords, 0.0, 1.0);
    pos = uTransform * gl_Position;
  }
</script>
