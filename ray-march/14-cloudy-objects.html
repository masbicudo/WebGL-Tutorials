<head>
    <title>#14 Cloudy objects</title>
    <script type="text/javascript" src="../lib/gl-matrix.js"></script>
    <script type="text/javascript" src="../lib/mdn-getShader.js"></script>
    <script type="text/javascript" src="../lib/leaf-draw.js"></script>
</head>
<body onload="javascript:startWebGLApp()">
    <div><canvas id="canvas" style="border:none" width="300" height="400" /></div>
    <div id="txt">0</div>
</body>
<script type="text/javascript">
  "use strict";

  function startWebGLApp() {
    var gl = getWebGLContext("canvas");
    var programs = {
      fragShader: getShader("fragment-shader"),
      vertexShader: getShader("vertex-shader")
    };
    var programParams = initShaders(gl);
    var shape = createSquare(gl);
    gl.clearColor(0, 0, 0, 1);
    draw(gl, shape, programParams);
  }

  function getWebGLContext(id) {
    var canvasElement = document.getElementById(id);
    try {
        var gl = canvas.getContext("webgl") || canvas.getContext("experimental-webgl");
        return gl;
    }
    catch(e) {}
    return;
  }

  function initShaders(gl) {
    var fragmentShader = getShader(gl, "fragment-shader");
    var vertexShader = getShader(gl, "vertex-shader");

    // Creating shader program
    var shaderProgram = gl.createProgram();
    gl.attachShader(shaderProgram, vertexShader);
    gl.attachShader(shaderProgram, fragmentShader);
    gl.linkProgram(shaderProgram);

    if (!gl.getProgramParameter(shaderProgram, gl.LINK_STATUS)) {
      alert("Could not initialize shaders.");
    }

    gl.useProgram(shaderProgram);

    var aVertexCoords = gl.getAttribLocation(shaderProgram, "aVertexCoords");
    gl.enableVertexAttribArray(aVertexCoords);

    var uTransform = gl.getUniformLocation(shaderProgram, "uTransform");
    var uTime = gl.getUniformLocation(shaderProgram, "uTime");

    return {
      aVertexCoords: aVertexCoords,
      uTransform: uTransform,
      uTime: uTime
    };
  }

  function createSquare(gl) {
    var squareVertexCoordBuffer = gl.createBuffer();
    var coorsdArray = [
      -1,  1, // 1st point
       1,  1, // 2nd point
      -1, -1, // 3rd point
       1, -1  // 4th point
    ];
    gl.bindBuffer(gl.ARRAY_BUFFER, squareVertexCoordBuffer);
    gl.bufferData(gl.ARRAY_BUFFER, new Float32Array(coorsdArray), gl.STATIC_DRAW);
    return {
      coordsBuffer: squareVertexCoordBuffer,
      coorsdArray: coorsdArray,
      pointCount: 4,
      pointSize: 2
    };
  }

  function draw(gl, shape, programParams) {
    gl.clear(gl.COLOR_BUFFER_BIT | gl.DEPTH_BUFFER_BIT);

    var ratio = canvas.width / canvas.height;
    var rx = ratio > 1 ? 1 : ratio;
    var ry = ratio > 1 ? 1/ratio : 1;
    var transformMatrix = [
         rx,   0,   0,   0,
          0,  ry,   0,   0,
          0,   0,   1,   0,
          0,   0,   0,   1
    ];
    gl.uniformMatrix4fv(programParams.uTransform, false, transformMatrix);

    // setting the current buffer
    gl.bindBuffer(gl.ARRAY_BUFFER, shape.coordsBuffer);

    // associate attribute 'aVertexCoords' with current buffer
    gl.vertexAttribPointer(programParams.aVertexCoords, 2, gl.FLOAT, false, 0, 0);

    var time = 0;
    var interval = 1000 / 15;
    window.setInterval(function() {

      gl.uniform1f(programParams.uTime, time * 0.01);
      time += interval;
      
      txt.innerHTML = time.toFixed(0);

      // draw triangles, using the shaders of the current program, with the current attributes
      gl.drawArrays(gl.TRIANGLE_STRIP, 0, shape.pointCount);

    }, interval);
  }

</script>
<script id="fragment-shader" type="x-shader/x-fragment">
  precision highp float;
  const int MAX_STEPS = 200;
  const vec3 LIGHT_AMBIENT = vec3(0.8,0.9,1.0)*0.99;
  const float ANIMATION_RATE = 1.0;
  const float ANIMATION_OFFSET = 20007.0;
  const float MIN_FOG_TRAVEL = 0.005;
  const float CLOUD_ABSORBTION = 0.005;
  const float CLOUD_INTENSITY = 8.0;
  const vec3 LIGHT_SOURCE = vec3(1.0, 1.0, -1.0);

  varying vec4 pos;

  uniform float uTime;

  const float PI = 3.14159265359;
  const float INFINITY = 1e20;
  const float Deg2Rad = 2.*PI/360.;

  float map(float value, float low1, float high1, float low2, float high2) {
    return low2 + (value - low1) * (high2 - low2) / (high1 - low1);
  }

  float distCircleNx(float n, vec2 p, vec2 c, float r) {
    vec2 k = p - c;
    return pow(pow(abs(k.x), n) + pow(abs(k.y), n), 1./n) - r;
  }
  vec2 mapPolarNx(float n, vec3 p) {
    return vec2(p.x, pow(pow(abs(p.y), n) + pow(abs(p.z), n), 1./n));
  }
  float distTorusNxx(float nt, float nc, vec3 p, float rt, float rc) {
    return distCircleNx(nc, mapPolarNx(nt, p), vec2(0., rt), rc);
  }

  vec3 rotateAroundY(vec3 v, float a) {
    float c_a = cos(a);
    float s_a = sin(a);
    mat4 T = mat4(
      c_a, 0.0, s_a, 0.0,
      0.0, 1.0, 0.0, 0.0,
     -s_a, 0.0, c_a, 0.0,
      0.0, 0.0, 0.0, 1.0
      );
    return (T * vec4(v, 1.0)).xyz;
  }
  vec3 rotateAroundX(vec3 v, float a) {
    float c_a = cos(a);
    float s_a = sin(a);
    mat4 T = mat4(
      1.0, 0.0, 0.0, 0.0,
      0.0, c_a,-s_a, 0.0,
      0.0, s_a, c_a, 0.0,
      0.0, 0.0, 0.0, 1.0
      );
    return (T * vec4(v, 1.0)).xyz;
  }
  vec3 translate(vec3 v, vec3 d) {
    mat4 T = mat4(
      1.0, 0.0, 0.0, 0.0,
      0.0, 1.0, 0.0, 0.0,
      0.0, 0.0, 1.0, 0.0,
      d.x, d.y, d.z, 1.0
    );
    return (T * vec4(v, 1.0)).xyz;
  }

  float distFunc(vec3 v) {
    v = translate(v, vec3(0.,0.,-1.));
    float t = uTime*ANIMATION_RATE + ANIMATION_OFFSET;
    v = rotateAroundX(v, (t*1.0 + 60.)*Deg2Rad);
    v = rotateAroundY(v, (t*1.0 + 30.)*Deg2Rad);

    vec3 v1 = v;
    v1 = translate(v1, vec3(0.,0.275,0.));
    v1 = rotateAroundX(v1, 90.*Deg2Rad);
    v1 = rotateAroundY(v1, 0.*Deg2Rad);
    float d1 = distTorusNxx(2., 2., v1, 0.4, 0.1);

    vec3 v2 = v;
    v2 = translate(v2, vec3(0.,-0.275,0.0));
    v2 = rotateAroundX(v2, -0.*Deg2Rad);
    v2 = rotateAroundY(v2, -90.*Deg2Rad);
    float d2 = distTorusNxx(10., 10., v2, 0.4, 0.1);

    return min(d1, d2);
  }

  vec3 raymarchFoggyLight(vec3 origin, vec3 vectorToLightSource) {
    vec3 p = origin;
    float distanceToLightSource = length(vectorToLightSource);
    vec3 lightSourceDirection = normalize(vectorToLightSource);

    float traveledSoFar = 0.0;
    vec3 fogFilter = vec3(1.0);

    for (int i = 0; i < MAX_STEPS; i++) {
      vec3 p2 = p + traveledSoFar*lightSourceDirection;
      float dist = distFunc(p2);
      float toTravel = max(abs(dist), MIN_FOG_TRAVEL);

      fogFilter *= vec3(pow(dist < 0.0 ? CLOUD_ABSORBTION : 1.0, toTravel));

      if (traveledSoFar > 2.0)
        break;
      traveledSoFar += toTravel;
    }
    
    return fogFilter;
  }

  // Returns the amount of traversed fog when tracing from the eye.
  vec4 raymarchFoggyEye(vec3 origin, vec3 travelDirection) {
    vec3 p = origin;

    float traveledSoFar = 0.0;
    vec3 fogFilter = vec3(1.0);
    vec3 totalLight = vec3(0.0);

    for (int i = 0; i < MAX_STEPS; i++) {
      vec3 p2 = p + traveledSoFar*travelDirection;
      float dist = distFunc(p2);
      float toTravel = max(abs(dist), MIN_FOG_TRAVEL);

      vec4 fogLight = dist < 0.0 ? vec4(raymarchFoggyLight(p2, LIGHT_SOURCE - p2), CLOUD_INTENSITY) : vec4(vec3(1.0), 0.0);;
      fogFilter *= vec3(pow(dist < 0.0 ? CLOUD_ABSORBTION : 1.0, toTravel));

      totalLight += fogLight.rgb * fogFilter * toTravel * fogLight.a;
      if (traveledSoFar > 2.0)
        break;
      traveledSoFar += toTravel;
    }
    
    return vec4(totalLight, 1.0);
  }
  void mainShaded(void) {
    vec3 origin = vec3(pos.xy, 0.0);
    vec3 direction = vec3(0.0, 0.0, 1.0);
    vec4 color = raymarchFoggyEye(
      origin,
      direction);

    gl_FragColor = vec4(color.rgb * LIGHT_AMBIENT, 1.0);
  }
  void main(void) {
    mainShaded(); return;
  }
</script>
<script id="vertex-shader" type="x-shader/x-vertex">
  attribute vec2 aVertexCoords;
  varying vec4 pos;
  uniform mat4 uTransform;

  void main(void) {
    gl_Position = vec4(aVertexCoords, 0.0, 1.0);
    pos = uTransform * gl_Position;
  }
</script>
