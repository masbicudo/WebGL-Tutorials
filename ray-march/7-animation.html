<head>
    <title>#7 Animation</title>
    <script type="text/javascript" src="../lib/gl-matrix.js"></script>
    <script type="text/javascript" src="../lib/mdn-getShader.js"></script>
</head>
<body onload="javascript:startWebGLApp()">
    <canvas id="canvas" style="border:none" width="600" height="800" />
</body>
<script type="text/javascript">
  "use strict";

  function startWebGLApp() {
    var gl = getWebGLContext("canvas");
    var programs = {
      fragShader: getShader("fragment-shader"),
      vertexShader: getShader("vertex-shader")
    };
    var programParams = initShaders(gl);
    var shape = createSquare(gl);
    gl.clearColor(0, 0, 0, 1);
    draw(gl, shape, programParams);
  }

  function getWebGLContext(id) {
    var canvasElement = document.getElementById(id);
    try {
        var gl = canvas.getContext("webgl") || canvas.getContext("experimental-webgl");
        return gl;
    }
    catch(e) {}
    return;
  }

  function initShaders(gl) {
    var fragmentShader = getShader(gl, "fragment-shader");
    var vertexShader = getShader(gl, "vertex-shader");

    // Creating shader program
    var shaderProgram = gl.createProgram();
    gl.attachShader(shaderProgram, vertexShader);
    gl.attachShader(shaderProgram, fragmentShader);
    gl.linkProgram(shaderProgram);

    if (!gl.getProgramParameter(shaderProgram, gl.LINK_STATUS)) {
      alert("Could not initialize shaders.");
    }

    gl.useProgram(shaderProgram);

    var aVertexCoords = gl.getAttribLocation(shaderProgram, "aVertexCoords");
    gl.enableVertexAttribArray(aVertexCoords);

    var uTransform = gl.getUniformLocation(shaderProgram, "uTransform");
    var uTime = gl.getUniformLocation(shaderProgram, "uTime");

    return {
      aVertexCoords: aVertexCoords,
      uTransform: uTransform,
      uTime: uTime
    };
  }

  function createSquare(gl) {
    var squareVertexCoordBuffer = gl.createBuffer();
    var coorsdArray = [
      -1,  1, // 1st point
       1,  1, // 2nd point
      -1, -1, // 3rd point
       1, -1  // 4th point
    ];
    gl.bindBuffer(gl.ARRAY_BUFFER, squareVertexCoordBuffer);
    gl.bufferData(gl.ARRAY_BUFFER, new Float32Array(coorsdArray), gl.STATIC_DRAW);
    return {
      coordsBuffer: squareVertexCoordBuffer,
      coorsdArray: coorsdArray,
      pointCount: 4,
      pointSize: 2
    };
  }

  function draw(gl, shape, programParams) {
    gl.clear(gl.COLOR_BUFFER_BIT | gl.DEPTH_BUFFER_BIT);

    var ratio = canvas.width / canvas.height;
    var rx = ratio > 1 ? 1 : ratio;
    var ry = ratio > 1 ? 1/ratio : 1;
    var transformMatrix = [
         rx,   0,   0,   0,
          0,  ry,   0,   0,
          0,   0,   1,   0,
          0,   0,   0,   1
    ];
    gl.uniformMatrix4fv(programParams.uTransform, false, transformMatrix);

    // setting the current buffer
    gl.bindBuffer(gl.ARRAY_BUFFER, shape.coordsBuffer);

    // associate attribute 'aVertexCoords' with current buffer
    gl.vertexAttribPointer(programParams.aVertexCoords, 2, gl.FLOAT, false, 0, 0);

    var time = 0;
    var interval = 1000 / 15;
    window.setInterval(function() {

      gl.uniform1f(programParams.uTime, time * 0.01);
      time += interval;

      // draw triangles, using the shaders of the current program, with the current attributes
      gl.drawArrays(gl.TRIANGLE_STRIP, 0, shape.pointCount);

    }, interval);
  }

</script>
<script id="fragment-shader" type="x-shader/x-fragment">
  precision highp float;
  const int MAX_STEPS = 100;
  const float CONTACT_DIFF = 2./1024.;
  const float NORMAL_GRADIENT_DIFF = 8./1024./1024.;
  const float LIGHT_AMBIENT = 0.075;
  const vec3 LIGHT_POINT = vec3(-3.0, 0.7, -0.7);
  const float MATERIAL_SHINESS = 5.;
  const float LIGHT_INTENSITY = 8.;

  varying vec4 pos;

  uniform float uTime;

  const float PI = 3.14159265359;
  const float Deg2Rad = 2.*PI/360.;

  float map(float value, float low1, float high1, float low2, float high2) {
    return low2 + (value - low1) * (high2 - low2) / (high1 - low1);
  }

  float distCircleNx(float n, vec2 p, vec2 c, float r) {
    vec2 k = p - c;
    return pow(pow(abs(k.x), n) + pow(abs(k.y), n), 1./n) - r;
  }
  vec2 mapPolarNx(float n, vec3 p) {
    return vec2(p.x, pow(pow(abs(p.y), n) + pow(abs(p.z), n), 1./n));
  }
  float distTorusNxx(float nt, float nc, vec3 p, float rt, float rc) {
    return distCircleNx(nc, mapPolarNx(nt, p), vec2(0., rt), rc);
  }

  vec3 rotateAroundY(vec3 v, float a) {
    float c_a = cos(a);
    float s_a = sin(a);
    mat4 T = mat4(
      c_a, 0.0, s_a, 0.0,
      0.0, 1.0, 0.0, 0.0,
     -s_a, 0.0, c_a, 0.0,
      0.0, 0.0, 0.0, 1.0
      );
    return (T * vec4(v, 1.0)).xyz;
  }
  vec3 rotateAroundX(vec3 v, float a) {
    float c_a = cos(a);
    float s_a = sin(a);
    mat4 T = mat4(
      1.0, 0.0, 0.0, 0.0,
      0.0, c_a,-s_a, 0.0,
      0.0, s_a, c_a, 0.0,
      0.0, 0.0, 0.0, 1.0
      );
    return (T * vec4(v, 1.0)).xyz;
  }
  vec3 translateZ(vec3 v, float d) {
    mat4 T = mat4(
      1.0, 0.0, 0.0, 0.0,
      0.0, 1.0, 0.0, 0.0,
      0.0, 0.0, 1.0, 0.0,
      0.0, 0.0,   d, 1.0
      );
    return (T * vec4(v, 1.0)).xyz;
  }

  float distFunc(vec3 v) {
    v = translateZ(v, -1.);
    v = rotateAroundX(v, 60.*Deg2Rad);
    v = rotateAroundY(v, 60.*Deg2Rad);
    v = rotateAroundY(v, (uTime*1. + 60.)*Deg2Rad);
    return distTorusNxx(2., 2., v, 0.4, 0.1);
  }

  void mainIterations(void) {
    vec3 p = vec3(pos.xy, 0.0);
    int steps = 0;
    for (int i = 0; i < MAX_STEPS; i++) {
        steps++;
        float dist = distFunc(p);
        p = vec3(p.xy, p.z + dist);
        if (dist < NORMAL_GRADIENT_DIFF) {
            break;
        }
    }
    gl_FragColor = vec4(vec3(float(steps)/float(MAX_STEPS)), 1.0);
  }
  void mainDepth(float minDepth, float maxDepth) {
    vec3 p = vec3(pos.xy, 0.0);
    int steps = 0;
    for (int i = 0; i < MAX_STEPS; i++) {
        steps++;
        float dist = distFunc(p);
        p = vec3(p.xy, p.z + dist);
        if (dist < NORMAL_GRADIENT_DIFF) {
            break;
        }
    }
    gl_FragColor = vec4(vec3(map(p.z, minDepth, maxDepth, 1., 0.)), 1.0);
  }
  void mainShaded(void) {
    vec3 p = vec3(pos.xy, 0.0);
    int steps = 0;
    vec3 normal = vec3(0.0, 0.0, 0.0);
    
    for (int i = 0; i < MAX_STEPS; i++) {
        steps++;
        float dist = distFunc(p);
        if (dist < NORMAL_GRADIENT_DIFF) {
            vec3 pX = vec3(p.x+CONTACT_DIFF, p.y, p.z);
            vec3 pY = vec3(p.x, p.y+CONTACT_DIFF, p.z);
            vec3 pZ = vec3(p.x, p.y, p.z+CONTACT_DIFF);
            float distX = distFunc(pX);
            float distY = distFunc(pY);
            float distZ = distFunc(pZ);

            // this will give the surface normal for the fragment
            normal = normalize(vec3(distX-dist, distY-dist, distZ-dist));

            break;
        }
        p = vec3(p.xy, p.z + dist);
    }

    if (steps == MAX_STEPS) {
      gl_FragColor = vec4(0.0, 0.0, 0.0, 1.0);
    } else {
      // Diffuse
      vec3 lightPos = LIGHT_POINT;
      vec3 lightDir = (lightPos - p);
      float lightIntensity = LIGHT_INTENSITY / dot(lightDir, lightDir);
      lightDir = normalize(lightDir);
      float diffuse = max(dot(normal, lightDir), 0.0);

      // Specular
      vec3 viewDir = normalize(vec3(pos.xy, 0.0) - p);
      vec3 reflectDir = reflect(-lightDir, normal);
      float specular = pow(max(dot(viewDir, reflectDir), 0.0), MATERIAL_SHINESS);

      float lightness = clamp((specular + diffuse) * lightIntensity + LIGHT_AMBIENT, 0.0, 1.0);
      gl_FragColor = vec4(lightness, lightness, lightness, 1.0);
    }
  }
  void main(void) {
    //mainDepth(0.5, 1.5); return;
    //mainIterations(); return;
    mainShaded(); return;
  }
</script>
<script id="vertex-shader" type="x-shader/x-vertex">
  attribute vec2 aVertexCoords;
  varying vec4 pos;
  uniform mat4 uTransform;

  void main(void) {
    gl_Position = vec4(aVertexCoords, 0.0, 1.0);
    pos = uTransform * gl_Position;
  }
</script>